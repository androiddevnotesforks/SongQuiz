plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'jacoco'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'androidx.navigation.safeargs.kotlin'
}

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.aaronfodor.android.songquiz"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 16
        versionName "v1.2.0"

        // spotify auth lib scheme and host of the redirect URI that the app is using for in app authorizing
        manifestPlaceholders = [redirectSchemeName: "spotify-sdk", redirectHostName: "auth"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions 'type'
    productFlavors {
        Free {
            dimension 'type'
            versionNameSuffix "-free"
        }
        Experimental {
            dimension 'type'
            versionNameSuffix "-experimental"
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
        debug {
            storeFile file('D:/Dev/MISC/Android Keystore/Debug/debug.keystore')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
            testCoverageEnabled false
            signingConfig signingConfigs.release

            resValue "string", "spotifyClientId", "51aee5a292494583852e135803681e05"
            resValue "string", "spotifyClientSec", ""
            // production ad values
            resValue "string", "adMobAppId", "ca-app-pub-5281333576177335~3104405139"
            resValue "string", "adMobQuizAdId", "ca-app-pub-5281333576177335/6311584294"
        }
        debug {
            debuggable true
            testCoverageEnabled true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"

            resValue "string", "spotifyClientId", "51aee5a292494583852e135803681e05"
            resValue "string", "spotifyClientSec", ""
            // test ad values
            resValue "string", "adMobAppId", "ca-app-pub-3940256099942544~3347511713"
            resValue "string", "adMobQuizAdId", "ca-app-pub-3940256099942544/1033173712"
        }
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
}

dependencies {
    // Kotlin lang
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    // App compat and UI
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'com.flaviofaria:kenburnsview:1.0.7'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Navigation library
    def nav_version = "2.4.1"
    implementation "androidx.navigation:navigation-fragment-ktx:${nav_version}"
    implementation "androidx.navigation:navigation-ui-ktx:${nav_version}"

    // viewBinding property delegate
    implementation 'com.github.kirich1409:viewbindingpropertydelegate:1.4.6'

    // DB
    def room_version = '2.4.2'
    implementation "androidx.room:room-runtime:${room_version}"
    kapt "androidx.room:room-compiler:${room_version}"

    // serialization
    implementation 'com.google.code.gson:gson:2.9.0'

    // network
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Spotify auth Maven dependency
    implementation 'com.spotify.android:auth:2.0.0'

    // settings
    def preference_version = "1.2.0"
    implementation "androidx.preference:preference-ktx:${preference_version}"

    // images
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // boarding
    implementation 'uk.co.samuelwall:material-tap-target-prompt:3.3.0'
    implementation 'com.github.AppIntro:AppIntro:6.2.0'

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Firebase, Analytics, Crashlytics
    implementation platform('com.google.firebase:firebase-bom:28.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Ads
    implementation 'com.google.android.gms:play-services-ads:20.6.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.mockito:mockito-core:2.24.5'
    testImplementation 'com.squareup.okhttp3:mockwebserver:5.0.0-alpha.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // debugImplementation because LeakCanary should only run in debug builds
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
}